subprojects {

    configurations {
        extender
        patch
    }

    dependencies {
        compile ("org.osgi:org.osgi.core:${project.ext.osgiVersion}")

        compileOnly("org.eclipse.gemini.blueprint:gemini-blueprint-extender:${project.ext.geminiVersion}") {
            exclude group: "org.springframework"
        }
        extender("org.eclipse.gemini.blueprint:gemini-blueprint-extender:${project.ext.geminiVersion}") {
            transitive = false
        }
    }

    project.tasks.remove(jar)

    task patch(type: Zip) {
        from(zipTree(configurations.extender.first())) {
            exclude("**/DefaultApplicationContextConfigurationFactory.class")
        }
        from(sourceSets.main.output)

        destinationDir = libsDir
        extension = 'jar'
        baseName = 'blueprint-synchronous'
    }

    artifacts {
        patch file: patch.archivePath, name: 'blueprint-synchronous', type: 'jar', builtBy: patch
    }

    defaultTasks 'patch'

}
