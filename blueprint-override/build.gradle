configurations {
    blueprint3Extender

    patchBlueprint3
}

dependencies {
    implementation("org.osgi:org.osgi.core:${project.ext.osgiVersion}")

    // Compile against the lowest version of Gemini Blueprint
    compileOnly("org.eclipse.gemini.blueprint:gemini-blueprint-extender:${geminiVersions.get('61')}") {
        exclude group: "org.springframework"
    }
    blueprint3Extender("org.eclipse.gemini.blueprint:gemini-blueprint-extender:${geminiVersions.get('61')}") {
        transitive = false
    }
}

task patchBlueprint3(type: Zip) {
    from(zipTree(configurations.blueprint3Extender.first())) {
        exclude("**/DefaultApplicationContextConfigurationFactory.class")
    }
    from(sourceSets.main.output)

    archiveExtension = 'jar'
    archiveBaseName = 'blueprint-3-synchronous'
}

artifacts {
    patchBlueprint3 file: patchBlueprint3.archivePath, name: 'blueprint-3-synchronous', type: 'jar', builtBy: patchBlueprint3
}

defaultTasks 'patchBlueprint3'
