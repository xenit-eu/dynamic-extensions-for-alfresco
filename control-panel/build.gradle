apply plugin: 'biz.aQute.bnd.builder'

configurations {
    testImplementation.extendsFrom(compileOnly)
}

dependencies {
    System.out.println("platform version for control-panel: "+alfrescoVersion)
    if (project.hasProperty("acsPackageVersion")) {
        def acsPackageVersion = project.property("acsPackageVersion")
        System.out.println("acsPackageVersion for control-panel: "+acsPackageVersion)
        compileOnly platform ("org.alfresco:acs-community-packaging:${acsPackageVersion}")
    }
    else {
        compileOnly platform ("org.alfresco:acs-community-packaging:${alfrescoVersion}")
    }

    implementation project(":alfresco-integration")
    implementation project(':annotations')
    implementation project(":webscripts")
    implementation project(":event-bus")

    compileOnly("org.alfresco.surf:spring-webscripts") { transitive = false }
    compileOnly("org.alfresco.surf:spring-surf-core") { transitive = false }

    compileOnly("org.alfresco:alfresco-data-model")
    compileOnly("org.alfresco:alfresco-repository")
    compileOnly("org.alfresco:alfresco-core")

    compileOnly "org.slf4j:slf4j-api"
    compileOnly "org.json:json"
    compileOnly "org.freemarker:freemarker"

    compileOnly('org.springframework:spring-beans') { transitive = false }
    compileOnly('org.springframework:spring-context') { transitive = false }
    compileOnly('org.springframework:spring-core') { transitive = false }
    compileOnly('org.springframework:spring-aop') { transitive = false }

    compileOnly "org.osgi:org.osgi.core:${project.ext.osgiVersion}"
    compileOnly "com.springsource.util:com.springsource.util.osgi:${project.ext.springOsgiUtilVersion}"

    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationApiVersion}"

    testImplementation("org.alfresco.surf:spring-surf-core-configservice") { transitive = false }

    testImplementation "org.mockito:mockito-core:${project.ext.mockitoVersion}"
    testImplementation "junit:junit:${project.ext.junitVersion}"
}

jar {
    bnd(
            'Bundle-Name': "${project.name}",
            'Bundle-SymbolicName': "${project.group}.${project.name}",
            'Alfresco-Dynamic-Extension': 'true',
            'Bundle-Description': 'Control Panel',
            'Alfresco-Spring-Configuration': 'com.github.dynamicextensionsalfresco.controlpanel',
            'Spring-Context': ';publish-context:=false',
            'Import-Package': 'javax.annotation;version=!, ' +
                    'org.aopalliance.aop, ' +
                    'org.aopalliance.intercept, ' +
                    'org.springframework.aop, ' +
                    'org.springframework.aop.framework, ' +
                    'org.slf4j;version="[1.7.2,2.1)", ' +
                    '* '
    )
}

defaultTasks 'jar'